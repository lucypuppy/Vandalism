plugins {
    id "fabric-loom" version "1.6-SNAPSHOT"
}

allprojects {
    apply plugin: "java-library"
    apply plugin: "fabric-loom"

    base {
        version = project.maven_version
        group = project.maven_group
    }

    configurations {
        jij // jar in jar configuration
        modJij // jar in jar configuration for mods

        include.extendsFrom modJij
        modImplementation.extendsFrom modJij
        modCompileOnlyApi.extendsFrom modJij
    }

    repositories {
        mavenCentral()

        maven {
            name = "ViaVersion"
            url = "https://repo.viaversion.com/"
        }

        maven {
            name = "Jitpack"
            url = "https://jitpack.io/"
        }

        maven {
            name = "Lenni0451"
            url = "https://maven.lenni0451.net/everything/"
        }

        maven {
            name = "TerraformersMC"
            url = "https://maven.terraformersmc.com/releases/"
        }

        maven {
            name = "OpenCollab Snapshots"
            url = "https://repo.opencollab.dev/maven-snapshots/"
        }

        maven {
            name = "Modrinth"
            url = "https://api.modrinth.com/maven/"
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${project.minecraft_version}"

        mappings "net.fabricmc:yarn:${project.yarn_mappings_version}:v2"
        modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

        modImplementation fabricApi.module("fabric-api-base", project.fabric_version)
        modImplementation fabricApi.module("fabric-resource-loader-v0", project.fabric_version)
        modImplementation fabricApi.module("fabric-networking-api-v1", project.fabric_version)
        modImplementation fabricApi.module("fabric-registry-sync-v0", project.fabric_version)
        modImplementation fabricApi.module("fabric-item-group-api-v1", project.fabric_version)

        modImplementation "de.florianmichael:ViaFabricPlus:${project.viafabricplus_version}"

        compileOnly "org.projectlombok:lombok:${project.lombok_version}"
        annotationProcessor "org.projectlombok:lombok:${project.lombok_version}"
    }

    processResources {
        filesMatching("fabric.mod.json") {
            expand "version": project.version
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    jar {
        //Rename the project's license file to LICENSE_<project_name> to avoid conflicts
        from("LICENSE") {
            rename {
                "${it}_${maven_name}"
            }
        }
    }

    idea {
        module {
            ["run"].each {
                excludeDirs << file("$projectDir/$it")
            }
        }
    }
}

subprojects {

    dependencies {
        implementation project(":")
    }

    tasks.register("installCompileAddon") {
        group "vandalism"

        dependsOn "build"
        finalizedBy "installAddon"
    }

    tasks.register("installAddon") {
        group "vandalism"

        doLast {
            //Locate the mods folder
            File modsFolder = new File(rootProject.projectDir, "run" + File.separator + "mods")
            modsFolder.mkdirs()

            //Addon output jars are always named Addon<name>-<version>.jar
            String newName = project.name + "-${project.maven_version}.jar"

            //Locate the output jar
            File outputJar = new File(file("."), "build" + File.separator + "libs" + File.separator + newName)

            for (final def modFile in modsFolder.listFiles()) {
                if (modFile.name.startsWith(project.name) && modFile.isFile()) {
                    //If the addon is already installed, delete the old version
                    if (!modFile.delete()) {
                        println "Failed to delete old version of " + project.name
                    }
                }
            }

            //Copy the output jar to the mods folder
            if (outputJar.renameTo(new File(modsFolder, newName))) {
                println "Installed " + project.name
            }
        }
    }

}

base {
    archivesName = project.maven_name
}

loom {
    accessWidenerPath = file("src/main/resources/" + maven_name.toLowerCase() + ".accesswidener")
}

dependencies {
    modJij "de.florianmichael:AsmFabricLoader:${project.asmfabricloader_version}"
    jij "de.florianmichael:DietrichEvents2:${project.dietrichevents2_version}"
    jij "de.florianmichael:WaybackAuthLib:${project.waybackauthlib_version}"

    jij "net.lenni0451:MCPing:${project.mc_ping_version}"
    jij "net.lenni0451:Reflect:${project.reflect_version}"

    jij "io.github.spair:imgui-java-binding:${project.spair_imgui_version}"
    jij ("io.github.spair:imgui-java-lwjgl3:${project.spair_imgui_version}") {
        exclude module: "lwjgl"
        exclude module: "lwjgl-glfw"
        exclude module: "lwjgl-opengl"
        exclude module: "lwjgl-bom"
    }
    jij "io.github.spair:imgui-java-natives-macos:${project.spair_imgui_version}"
    jij "io.github.spair:imgui-java-natives-windows:${project.spair_imgui_version}"
    jij "io.github.spair:imgui-java-natives-linux:${project.spair_imgui_version}"

    jij "net.raphimc:NoteBlockLib:${project.noteblocklib_version}"
    jij "net.raphimc:MinecraftAuth:${project.minecraftauth_version}"

    // Fabric's jar in jar system doesn't support transitive dependencies, so we have to manually add them
    afterEvaluate {
        configurations.jij.incoming.resolutionResult.allDependencies {
            dependencies.include(dependencies.implementation(dependencies.compileOnlyApi(requested.toString())))
        }
    }
}
